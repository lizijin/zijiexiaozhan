apply plugin: 'com.jfrog.bintray'

version = '1.0.1'

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load( new FileInputStream("local.properties"))

// Bintray
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    pkg {
        repo = 'sticky-header-recyclerview'
        name = 'sticky-header-recyclerview'
        configurations = ['archives']
        desc = 'RecyclerView吸顶库.'
        websiteUrl = 'https://github.com/lizijin/StickyHeaderForRecyclerView'
        vcsUrl = 'https://github.com/lizijin/StickyHeaderForRecyclerView.git'
        licenses = ["Apache-2.0"]
        publish = true
        publicDownloadNumbers = true
    }
}